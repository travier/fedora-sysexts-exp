name := "cilium-cli"
base_images := "
quay.io/fedora/fedora-coreos:stable x86_64,aarch64
quay.io/fedora/fedora-coreos:next x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get the binary
download-manual arch=arch:
    #!/bin/bash
    set -euo pipefail
    # set -x

    mkdir binaries
    cd binaries

    echo "⬇️ Downloading cilium-cli"

    VERSION=$(curl -s https://raw.githubusercontent.com/cilium/cilium-cli/main/stable.txt)

    if [[ "{{arch}}" == "x86_64" ]]; then
        ARCH=amd64
    else
        ARCH=arm64
    fi

    curl -L --fail --remote-name-all https://github.com/cilium/cilium-cli/releases/download/${VERSION}/cilium-linux-${ARCH}.tar.gz{,.sha256sum}
    sha256sum --check cilium-linux-${ARCH}.tar.gz.sha256sum
    tar xzvfC cilium-linux-${ARCH}.tar.gz ./

    curl -L --fail --remote-name-all "https://github.com/cilium/cilium-cli/raw/refs/tags/${VERSION}/LICENSE"

    # Metadata
    echo "${VERSION#v}" > ../version
    sha256sum cilium-linux-${ARCH}.tar.gz LICENSE >> ../inputs

# Custom installation step to install the binary
install-manual:
    #!/bin/bash
    set -euo pipefail
    # set -x

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing cilium-cli"
    ${SUDO} install -D -m 755 ../binaries/cilium  usr/bin/cilium
    ${SUDO} install -D -m 644 ../binaries/LICENSE usr/share/licenses/cilium/LICENSE

    ${SUDO} chown -R root: usr
