name := "cilium-cli"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get the binary
download-manual arch=arch:
    #!/bin/bash
    set -euo pipefail
    # set -x

    mkdir binaries
    cd binaries

    echo "⬇️ Downloading cilium-cli"

    version=$(curl -s https://raw.githubusercontent.com/cilium/cilium-cli/main/stable.txt)

    if [[ "{{arch}}" == "x86_64" ]]; then
        arch=amd64
    else
        arch=arm64
    fi

    curl --location --fail --output "cilium-linux-${arch}.tar.gz" \
        "https://github.com/cilium/cilium-cli/releases/download/${version}/cilium-linux-${arch}.tar.gz"
    curl --location --fail --output "cilium-linux-${arch}.tar.gz.sha256sum" \
        "https://github.com/cilium/cilium-cli/releases/download/${version}/cilium-linux-${arch}.tar.gz.sha256sum"
    sha256sum --check cilium-linux-${arch}.tar.gz.sha256sum
    tar xzvfC cilium-linux-${arch}.tar.gz ./

    curl --location --fail --output "LICENSE" \
        "https://github.com/cilium/cilium-cli/raw/refs/tags/${version}/LICENSE"

    # Version & metadata
    echo "${version#v}" > ../version
    sha256sum cilium-linux-${arch}.tar.gz LICENSE >> ../inputs

# Skip default recipe
version:
    true

# Custom installation step to install the binary
install-manual:
    #!/bin/bash
    set -euo pipefail
    # set -x

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing cilium-cli"
    ${SUDO} install -D -m 755 ../binaries/cilium  usr/bin/cilium
    ${SUDO} install -D -m 644 ../binaries/LICENSE usr/share/licenses/cilium/LICENSE

    ${SUDO} chown -R root: usr
