name := "1password-gui"
packages := "1password"
pre_commands := "rm -rf /opt; mkdir /opt"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64
"

import '../sysext.just'

all: default

download-rpms target arch=arch:
    #!/bin/bash
    set -euo pipefail
    # set -x
    mkdir -p rpms
    curl --location --fail --output rpms/1password.rpm \
        "https://downloads.1password.com/linux/rpm/stable/x86_64/1password-latest.rpm"

install-manual:
    #!/bin/bash
    set -euo pipefail
    # set -x

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    ${SUDO} mv opt/1Password usr/lib/1Password
    ${SUDO} rmdir opt
    ${SUDO} mkdir usr/bin
    ${SUDO} ln -sf /usr/lib/1Password/1password usr/bin/1password
    ${SUDO} ln -sf /usr/lib/1Password/1Password-BrowserSupport usr/bin/1Password-BrowserSupport
    ${SUDO} ln -sf /usr/lib/1Password/1Password-Crash-Handler usr/bin/1Password-Crash-Handler
    ${SUDO} chmod 4755 usr/lib/1Password/chrome-sandbox
    ${SUDO} sed -i 's|^Exec=/opt/1Password|Exec=/usr/bin|g' usr/share/applications/1password.desktop
